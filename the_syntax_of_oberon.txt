http://oberon07.com/EBNF.txt

EBNF of Oberon-07 (David Egan Evans, 20160727)
-=-=-

Following is a rearrangement of the Oberon-07 EBNF. This reflects the official
EBNF of the Oberon Report (not the Appendix), including the Project Oberon 2013
change to CASE as a type guard for RECORD extension.


"character" refers to a member of type CHAR.

-letter = "A" | "B" | ... | "Z" | "a" | "b" | ... | "z".
-digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9".
^^^ - отдает еще лекс
+hexDigit = digit | "A" | "B" | "C" | "D" | "E" | "F".
+integer = digit {digit} | digit {hexDigit} "H".
состоит из двух
integer_dec и integer_hex
+ident = letter {letter | digit}.
ScaleFactor = "E" ["+" | "-"] digit {digit}.
+real = digit {digit} "." {digit} [ScaleFactor].
+string = """ {character} """ | digit {hexDigit} "X".
+number = integer | real.
MulOperator = "*" | "/" | DIV | MOD | "&".
import = ident [":=" ident].
AddOperator = "+" | "-" | OR.
qualident = [ident "."] ident.
BaseType = qualident.
label = integer | string | qualident.
relation = "=" | "#" | "<" | "<=" | ">" | ">=" | IN | IS.
LabelRange = label [".." label].
identdef = ident ["*"].
CaseLabelList = LabelRange {"," LabelRange}.
IdentList = identdef {"," identdef}.
ImportList = IMPORT import {"," import} ";".
FormalType = {ARRAY OF} qualident.
FPSection = [VAR] ident {"," ident} ":" FormalType.
type = qualident | StrucType.
PointerType = POINTER TO type.
ConstExpression = expression.
length = ConstExpression.
VariableDeclaration = IdentList ":" type.
FieldList = IdentList ":" type.
ConstDeclaration = identdef "=" ConstExpression.
TypeDeclaration = identdef "=" StrucType.
assignment = designator ":=" expression.
ProcedureType = PROCEDURE [FormalParameters].
StrucType = ArrayType | RecordType | PointerType | ProcedureType.
element = expression [".." expression].
ProcedureCall = designator [ActualParameters].
case = [CaseLabelList ":" StatementSequence].
selector = "." ident | "[" ExpList "]" | "^" | "(" qualident ")".
factor = number | string | NIL | TRUE | FALSE | set | designator [ActualParameters] | "(" expression ")" | "~" factor.
ActualParameters = "(" [ExpList] ")" .
module = MODULE ident ";" [ImportList] DeclarationSequence [BEGIN StatementSequence] END ident "." .
RepeatStatement = REPEAT StatementSequence UNTIL expression.
expression = SimpleExpression [relation SimpleExpression].
statement = [assignment | ProcedureCall | IfStatement | CaseStatement | WhileStatement | RepeatStatement | ForStatement].
ForStatement = FOR ident ":=" expression TO expression [BY ConstExpression] DO StatementSequence END.
ProcedureHeading = PROCEDURE identdef [FormalParameters].
ProcedureDeclaration = ProcedureHeading ";" ProcedureBody ident.
RecordType = RECORD ["(" BaseType ")"] [FieldListSequence] END.
ProcedureBody = DeclarationSequence [BEGIN StatementSequence] [RETURN expression] END.

{} непроверенное
SimpleExpression = ["+" | "-"] term {AddOperator term}.
term = factor {MulOperator factor}.
designator = qualident {selector}.
ExpList = expression {"," expression}.
StatementSequence = statement {";" statement}.
FieldListSequence = FieldList {";" FieldList}.


DeclarationSequence = [CONST {ConstDeclaration ";"}] [TYPE {TypeDeclaration ";"}] [VAR {VariableDeclaration ";"}] {ProcedureDeclaration ";"}.
FormalParameters = "(" [FPSection {";" FPSection}] ")" [":" qualident].
set = "{" [element {"," element}] "}".
ArrayType = ARRAY length {"," length} OF type.
IfStatement = IF expression THEN StatementSequence {ELSIF expression THEN StatementSequence} [ELSE StatementSequence] END.
CaseStatement = CASE expression OF case {"|" case} END.
WhileStatement = WHILE expression DO StatementSequence {ELSIF expression DO StatementSequence} END.
